plugins {
  id 'java'
  id 'maven-publish'
  id 'io.freefair.lombok' version '5.3.0'
}

description = 'Apereo CAS WebAuthn Server Helper'

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      groupId project.group
      artifactId project.name
      version project.version
    }
  }

  repositories {
      if (project.hasProperty("publishReleases")) {
          maven {
              name "SonatypeReleases"
              url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
              mavenContent {
                  releasesOnly()
              }
              credentials(PasswordCredentials)
          }
      }
      if (project.hasProperty("publishSnapshots")) {
          maven {
              name "SonatypeSnapshots"
              url "https://oss.sonatype.org/content/repositories/snapshots/"
              mavenContent {
                  snapshotsOnly()
              }
              credentials(PasswordCredentials)
          }
      }
  }
}

repositories {
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
    mavenContent { snapshotsOnly() }
  }
  maven {
    mavenContent { releasesOnly() }
    url "https://build.shibboleth.net/nexus/content/repositories/releases/"
  }
}

dependencies {
  implementation "com.yubico:webauthn-server-attestation:${project.yubikeyWebAuthnVersion}"
  implementation "com.yubico:webauthn-server-core:${project.yubikeyWebAuthnVersion}"
  implementation "com.yubico:yubico-util:${project.yubikeyWebAuthnVersion}"

  compileOnly "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"
  compileOnly "com.google.guava:guava:${guavaVersion}"
  compileOnly "org.slf4j:slf4j-api:${slf4jVersion}"
  implementation "com.upokecenter:cbor:${cborVersion}"
}
